plugins {
    id 'net.neoforged.gradle.userdev' version '7.1.20' apply false
}

// Only configure leaf version subprojects (skip intermediate ':versions' project)
subprojects { sub ->
    if (!sub.file('gradle.properties').exists()) return
    apply plugin: 'net.neoforged.gradle.userdev'

    version = mod_version
    group = 'com.minaret'
    base.archivesName = "minaret-${minecraft_version}"

    java.toolchain.languageVersion = JavaLanguageVersion.of(21)

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        implementation "net.neoforged:neoforge:${neo_version}"
    }

    sourceSets.main {
        java.srcDirs = [rootProject.file('src/main/java')]
        resources.srcDirs = [rootProject.file('src/main/resources')]
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    processResources {
        var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
            pack_format            : pack_format,
        ]
        inputs.properties replaceProperties

        filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
            expand replaceProperties
        }
    }
}
